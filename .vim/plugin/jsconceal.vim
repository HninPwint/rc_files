command! JSConcealOn :call s:JSConcealOn()
command! JSConcealOff :call s:JSConcealOff()
command! JSConcealCycle :call s:JSConcealCycle()

let g:JSConcealCrazinessLevel = get(g:, 'JSConcealCrazinessLevel', 3)

function! s:JSConcealCycle()
  let g:JSConcealCrazinessLevel = (g:JSConcealCrazinessLevel + 1) % 11
  call s:JSConcealOn()
  echo "Craziness=".g:JSConcealCrazinessLevel." @: to cycle"
endfunction

function! s:JSConcealOn()
  call s:JSConcealOff()

  let level = g:JSConcealCrazinessLevel

  syn clear javascriptThis
  syn clear javascriptIdentifier
  syn match   jsNiceThis       /\<this\(\.\|\>\)/ conceal cchar=@
  syn match   jsNicePrototype  /\.prototype\./  conceal cchar=‚à∑
  " forAll: ‚àÄ
  " null: ‚àÖ‚äò
  " undefined: ‚àÑ ? ‚äó ‚à§
  if level > 0
    syn keyword jsNiceFunction   function skipwhite conceal cchar=ùíá "ŒªùëìùêüùêÖùë≠ùóôùó≥
  endif
  if level > 3
    syn clear javascriptAssignVar
    syn clear javascriptAssignment
    " Regardless of level, /=/ must be defined before the other matches on =, or it will override them.
    " OK now we have refined it from /=/ a bit
    syn match   jsNiceAssign     /[^=!<>]==\@!/ms=s+1   conceal cchar=‚Ü§ "‚âî‚Üê‚áê‚á¶‚¨Ö‚á§‚§ô‚ùÆ¬´‚äõ
  endif
  if level > 2
    syn keyword jsNiceReturn     return conceal cchar=‚§∫ "‚¨ë‚Ü∂‚Ü≤‚èé‚á¶‚§Ü‚áê‚Ü©‚á§‚¨Ö‚Üñ‚¨â‚áô‚¨ã‚â™‚¨é‚Üµ‚Ü§‚Ü¢‚Ü©‚Üª‚áô‚á±‚óÖ‚óÄ
    syn match   jsNiceApprox     /==/  conceal cchar=‚âà "‚àº‚àø
    syn match   jsNiceNotApprox  /!=/  conceal cchar=‚ââ
    syn match   jsNiceEqual      /===/ conceal cchar=‚â° "=
    syn match   jsNiceNotEqual   /!==/ conceal cchar=‚â¢ "‚â†
    " ‚â§ and ‚â• and ‚â∫‚âª‚âº‚âΩ
    syn match   jsNiceKeyword    /\<Math\.PI\>/ conceal cchar=œÄ
    syn match   jsNiceKeyword    /\<Math\.sqrt\>/ conceal cchar=‚àö
    syn match   jsNiceOperator   /\<Infinity\>/ conceal cchar=‚àû
  endif
  if level > 4
    syn keyword jsNiceVar        var   conceal cchar=‚ñº "‚àÉùíóùíçùë≥ùêïùëΩùó©ùêØùòÉ‚òÄ‚öô‚ö™‚ö´‚óè‚ô∞‚ò™‚òæ‚ö°‚ò™‚ô•‚ô¶‚ôù‚òõ‚öπ‚ô¶‚ñ∂‚óÄ‚ß®‚ó•‚ñæ
  endif
  if level > 5
    syn keyword jsNiceBoolean    true  conceal cchar=‚úî "‚òë‚ä§
    syn keyword jsNiceBoolean    false conceal cchar=‚úò "‚òí‚ä•
  endif
  if level > 8
    syn match   jsNiceLogic      /||/  conceal cchar=‚ãé  "‚à®‚ãÅ
    syn match   jsNiceLogic      /&&/  conceal cchar=‚ãè  "‚àß‚ãÄ
    syn match   jsNiceConsole    /\<\(debugger\|console\.\(log\|warn\|info\|error\)\)\>/ conceal cchar=‚ò¢ "‚öê‚öë‚ò£‚ò†
  endif
  if level > 9
    syn match   jsNiceSemicolon  /;/ conceal cchar=‚òª "‚ò∫
    "syn match   jsNiceComma      /,/ conceal cchar=‚ò∫
  endif
  setlocal conceallevel=2
endfunction

function! s:JSConcealOff()
  silent! syn clear jsNiceFunction
  silent! syn clear jsNiceThis
  silent! syn clear jsNicePrototype
  silent! syn clear jsNiceReturn
  silent! syn clear jsNiceAssign
  silent! syn clear jsNiceApprox
  silent! syn clear jsNiceNotApprox
  silent! syn clear jsNiceEqual
  silent! syn clear jsNiceNotEqual
  silent! syn clear jsNiceVar
  silent! syn clear jsNiceBoolean
  silent! syn clear jsNiceLogic
  silent! syn clear jsNiceConsole
  silent! syn clear jsNiceSemicolon
  silent! syn clear jsNiceComma
endfunction

